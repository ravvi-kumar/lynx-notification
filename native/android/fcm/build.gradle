plugins {
  id 'com.android.library' version '8.7.3'
  id 'maven-publish'
}

group = 'io.lynx.notifications'
version = '0.1.0-alpha'

def mavenPublishUrl = findProperty('mavenPublishUrl') ?: System.getenv('MAVEN_PUBLISH_URL')
def mavenPublishUsername = findProperty('mavenPublishUsername') ?: System.getenv('MAVEN_PUBLISH_USERNAME')
def mavenPublishPassword = findProperty('mavenPublishPassword') ?: System.getenv('MAVEN_PUBLISH_PASSWORD')

android {
  namespace 'io.lynx.notifications.fcm'
  compileSdk 34

  defaultConfig {
    minSdk 24
    consumerProguardFiles 'consumer-rules.pro'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  publishing {
    singleVariant('release') {
      withSourcesJar()
    }
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
}

dependencies {
  implementation 'io.lynx.notifications:core:0.1.0-alpha'
  implementation 'com.google.firebase:firebase-messaging:24.0.0'
}

afterEvaluate {
  publishing {
    publications {
      fcm(MavenPublication) {
        groupId = 'io.lynx.notifications'
        artifactId = 'fcm'
        version = project.version
        from components.release
      }
    }
    repositories {
      mavenLocal()
      if (mavenPublishUrl) {
        maven {
          name = 'remote'
          url = uri(mavenPublishUrl)
          if (mavenPublishUsername && mavenPublishPassword) {
            credentials {
              username = mavenPublishUsername
              password = mavenPublishPassword
            }
          }
        }
      }
    }
  }
}
