plugins {
  id 'com.android.library' version '8.7.3'
  id 'maven-publish'
}

group = 'io.lynx.notifications'
version = '0.1.0-alpha'

def mavenPublishUrl = findProperty('mavenPublishUrl') ?: System.getenv('MAVEN_PUBLISH_URL')
def mavenPublishUsername = findProperty('mavenPublishUsername') ?: System.getenv('MAVEN_PUBLISH_USERNAME')
def mavenPublishPassword = findProperty('mavenPublishPassword') ?: System.getenv('MAVEN_PUBLISH_PASSWORD')

android {
  namespace 'io.lynx.notifications.android'
  compileSdk 34

  defaultConfig {
    minSdk 24
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    consumerProguardFiles 'consumer-rules.pro'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  testOptions {
    animationsDisabled = true
  }

  publishing {
    singleVariant('release') {
      withSourcesJar()
    }
  }
}

repositories {
  mavenLocal()
  google()
  mavenCentral()
}

dependencies {
  api 'io.lynx.notifications:core:0.1.0-alpha'
  implementation 'androidx.activity:activity:1.9.3'
  implementation 'androidx.core:core:1.15.0'
  androidTestImplementation 'androidx.test:core:1.6.1'
  androidTestImplementation 'androidx.test.ext:junit:1.2.1'
  androidTestImplementation 'androidx.test:runner:1.6.2'
}

afterEvaluate {
  publishing {
    publications {
      runtime(MavenPublication) {
        groupId = 'io.lynx.notifications'
        artifactId = 'android-runtime'
        version = project.version
        from components.release
      }
    }
    repositories {
      mavenLocal()
      if (mavenPublishUrl) {
        maven {
          name = 'remote'
          url = uri(mavenPublishUrl)
          if (mavenPublishUsername && mavenPublishPassword) {
            credentials {
              username = mavenPublishUsername
              password = mavenPublishPassword
            }
          }
        }
      }
    }
  }
}
