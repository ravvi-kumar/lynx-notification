plugins {
  id 'java-library'
  id 'maven-publish'
}

group = 'io.lynx.notifications'
version = '0.1.0-alpha'

def mavenPublishUrl = findProperty('mavenPublishUrl') ?: System.getenv('MAVEN_PUBLISH_URL')
def mavenPublishUsername = findProperty('mavenPublishUsername') ?: System.getenv('MAVEN_PUBLISH_USERNAME')
def mavenPublishPassword = findProperty('mavenPublishPassword') ?: System.getenv('MAVEN_PUBLISH_PASSWORD')

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  testImplementation 'junit:junit:4.13.2'
}

publishing {
  publications {
    core(MavenPublication) {
      groupId = 'io.lynx.notifications'
      artifactId = 'core'
      version = project.version
      from components.java
    }
  }
  repositories {
    mavenLocal()
    if (mavenPublishUrl) {
      maven {
        name = 'remote'
        url = uri(mavenPublishUrl)
        if (mavenPublishUsername && mavenPublishPassword) {
          credentials {
            username = mavenPublishUsername
            password = mavenPublishPassword
          }
        }
      }
    }
  }
}
